group 'com.rover12421'
version '1.0'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVer"
    }
}

def loadProperties(String sourceFileName) {
    def config = new Properties()
    def propFile = new File(sourceFileName)
    System.err.println("Loading property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            System.out.println("setting " + property.key)
            project.ext[property.key] = property.value;
        }
    }
}

loadProperties("local.properties")

// used for official releases only. Please don't use
task release {
}

def maven_release_projects = ['fulvakt']

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = jdkVer
    targetCompatibility = jdkVer
}

subprojects {
    ext {
        depends = [
                junit           : 'junit:junit:4.12',
                kotlin_stdlib   : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer",
        ]
    }

    dependencies {
        compile depends.kotlin_stdlib

        testCompile depends.junit
    }

    if (project.name in maven_release_projects) {
        apply plugin: 'maven'
        apply plugin: 'signing'
        apply plugin: 'org.jetbrains.dokka'

        dokka {
            moduleName = project.name
            outputFormat = 'javadoc'
            outputDirectory = "$buildDir/javadoc"
            processConfigurations = ['compile', 'extra']
            includes = ['packages.md', 'extra.md']
            samples = ['samples/basic.kt', 'samples/advanced.kt']
            linkMapping {
                dir = "src/main/kotlin"
                url = "https://github.com/cy6erGn0m/vertx3-lang-kotlin/blob/master/src/main/kotlin"
                suffix = "#L"
            }
            sourceDirs = sourceSets.main.allSource
        }

        task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
            outputFormat = 'javadoc'
            outputDirectory = javadoc.destinationDir
            inputs.dir 'src/main/kotlin'
        }

        task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar
            archives sourcesJar
        }

        signing {
            required { gradle.taskGraph.hasTask('uploadArchives') }
            sign configurations.archives
        }

        uploadArchives {
            repositories.mavenDeployer {
                configuration = configurations.archives

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (rootProject.hasProperty('sonatypeUsername') && rootProject.hasProperty('sonatypePassword')) {
                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }
                }

                pom.artifactId = project.name

                pom.project {
                    name project.name
                    url 'http://www.rover12421.com'
                    packaging 'jar'
                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }
                    scm {
                        connection 'scm:git:git://github.com/rover12421/FulvaKt.git'
                        developerConnection 'scm:git:git@github.com:rover12421/FulvaKt.git'
                    }
                    developers {
                        developer {
                            id 'rover12421'
                            name 'Rover12421'
                            email 'rover12421@163.com'
                        }
                    }
                }
            }
        }

        tasks.getByPath(':release').dependsOn(uploadArchives)
    }
}

idea {
    project {
        languageLevel = jdkVer
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

